# Maintainer: Jan Marvin Garbuszus <jan.garbuszus@ruhr-uni-bochum.de>
# Inspired by https://aur.archlinux.org/packages.php?ID=47225
#

verdate=$(date +"%Y%m%d")

pkgname=rstudio-electron-git
pkgver=$verdate
pkgrel=1
pkgdesc="rstudio git release pkgbuild by jmg"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('AGPL3')
depends=('r>=3.0.1' 'boost-libs>=1.50' 'clang' 'soci' 'postgresql-libs' 'sqlite' 'yaml-cpp')
makedepends=('git' 'cmake>=2.6' 'boost>=1.50' 'jdk11-openjdk' 'apache-ant' 'unzip' 'openssl' 'pam' 'mathjax2' 'rrdtool' 'yarn')
provides=('rstudio-desktop')
conflicts=('rstudio-desktop')
install=rstudio.install
quartover='0.9.80'
source=('rstudio::git+https://github.com/rstudio/rstudio.git'
        "https://github.com/quarto-dev/quarto-cli/releases/download/v$quartover/quarto-$quartover-linux-amd64.tar.gz"
        'rstudio.desktop'
        'rstudio.png'
        'node_path.diff'
        )
md5sums=('SKIP'
         '2e1d43e87a80822ef1d83041afbb2205'
         '07b7333a588e5629f86b7dc3fba79ecf'
         '99ad4e9914f50ecec97e5cdb4001ad29'
         'fee83f7711bcce0b9843e0abe30035fa')


prepare() {
  cd "$srcdir/rstudio"
  git reset --hard
  
  msg "fix cmake install path"
  git apply ../node_path.diff

  # make sure icons and mime stuff are with prefix
  sed -i \
    -e "s:/usr:$pkgdir/usr:g" \
    CMakeGlobals.txt src/cpp/desktop/CMakeLists.txt || die

  # get version information from git
  cd "$srcdir/rstudio"
  _gittag=$(git describe --tags $(git rev-list --tags --max-count=1))

  gittag=${_gittag#v*}
  major=${gittag%.*.*}
  gittag=${gittag#$major.*}
  minor=${gittag%.*}
  patch=${gittag#*.}

  echo "${major}.${minor}.${patch}"

  msg "install-packages and Pandoc"
  cd "$srcdir/rstudio/dependencies/"
  # Create empty dictionaries folder
  mkdir -p "dictionaries"
  touch "dictionaries/.aff"
  touch "dictionaries/.dic"

  # get nodever 
  nodever=$(grep "RSTUDIO_NODE_VERSION=" tools/rstudio-tools.sh | cut -d '"' -f2)
  
  # Add mathjax & quarto (this #!@*&% does not bother me at all)
  rm -rf "mathjax-27" 
  ln -s "/usr/share/mathjax2" "mathjax-27"

  rm -rf "quarto" && mkdir -p "quarto"
  cp -r $srcdir/"quarto-$quartover"/{bin,share} "quarto"

  cd "common/"
  ./install-packages

  # Nodejs: use any nodejs available (legacy or current does not seem to matter)
  rm -rf node
  mkdir -p node/${nodever}/{bin,include}
  ln -s /usr/bin/node node/${nodever}/bin
  ln -s /usr/bin/npm node/${nodever}/bin
  ln -s /usr/bin/npx node/${nodever}/bin
  ln -s /usr/include/node node/${nodever}/include
  cd "${srcdir}/rstudio/src/gwt/panmirror/src/editor"
  yarn config set ignore-engines true
  yarn install --arch=x64

  # version information in rstudio
  sed -i -e "s/CPACK_PACKAGE_VERSION_MAJOR \"99\"/CPACK_PACKAGE_VERSION_MAJOR \"$major\"/g; \
             s/CPACK_PACKAGE_VERSION_MINOR \"9\"/CPACK_PACKAGE_VERSION_MINOR \"$minor\"/g; \
             s/CPACK_PACKAGE_VERSION_PATCH \"9\"/CPACK_PACKAGE_VERSION_PATCH \"$patch\"/g" \
             "$srcdir/rstudio/CMakeGlobals.txt"

}

build(){

  # https://github.com/rstudio/rstudio/issues/9463
  export PATH=/usr/lib/jvm/java-11-openjdk/bin/:${PATH}
  
  cmake -B build -S $srcdir/rstudio \
        -DRSTUDIO_TARGET=Electron \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio \
        -DRSTUDIO_USE_SYSTEM_BOOST=yes \
        -DRSTUDIO_USE_SYSTEM_SOCI=yes \
        -DRSTUDIO_USE_SYSTEM_YAML_CPP=yes\
        ..

  msg "Starting make"
  cmake --build build -j16
  
}

package() {

  msg "make install"
  DESTDIR="$pkgdir" cmake --install build

  rm -rf "$pkgdir/usr/lib/rstudio/R/packages/"*.tar.gz

  install -Dm644 "$srcdir/rstudio.desktop" "$pkgdir/usr/share/applications/rstudio.desktop"
  install -Dm644 "$srcdir/rstudio.png" "$pkgdir/usr/share/pixmaps/rstudio.png"

  msg "Cleaning Sources â€¦"
  rm -rf "$srcdir/rstudio/build"
  rm -r "$srcdir/rstudio/dependencies/dictionaries/"
  rm -r "$srcdir/rstudio/dependencies/mathjax-27"
}

# vim:set ts=2 sw=2 et:
