# Maintainer: Jan Marvin Garbuszus <jan.garbuszus@ruhr-uni-bochum.de>
# Inspired by https://aur.archlinux.org/packages.php?ID=47225
#

verdate=$(date +"%Y%m%d")

pkgname=rstudio-desktop-git
pkgver=$verdate
pkgrel=1
pkgdesc="rstudio git release pkgbuild by jmg"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('AGPL3')
depends=('r>=3.0.1' 'boost-libs>=1.50' 'clang' 'soci' 'postgresql-libs' 'sqlite' 'pandoc' 'yaml-cpp')
makedepends=('git' 'cmake>=2.6' 'boost>=1.50' 'java-environment' 'apache-ant' 'unzip' 'openssl' 'pam' 'mathjax2' 'rrdtool' 'nodejs' 'yarn')
provides=('rstudio-desktop')
conflicts=('rstudio-desktop')
install=rstudio.install
source=('rstudio.desktop'
        'rstudio.png'
        'prefs.patch'
        'qt_lib.patch'
        'boost.patch'
        )
md5sums=('07b7333a588e5629f86b7dc3fba79ecf'
         '99ad4e9914f50ecec97e5cdb4001ad29'
         'b7ca2e015536f4df360b26322ccc2d72'
         '92f0611916adab94abbdfb245c4689ab'
         '83df084fa6423696d67aad300f6474c0')

_gitroot="git://github.com/rstudio/rstudio.git"
_gitname="rstudio"

prepare() {
  cd $srcdir/..

  # Fetch Sources
  if [ -d "$_gitname" ] ; then
    msg "Updating repository from server …"
    cd "$_gitname"
    # git reset required for the sed-gin-hack
    git reset --hard && git pull

    git checkout master
    # git checkout v1.3
  else
    msg "Cloning from GIT server …"
    git clone "$_gitroot" "$_gitname"
  fi

  cd "$srcdir"
  rm -rf rstudio
  git clone ../$_gitname
  cd $_gitname

  # boost fixes. boostsignals2 and includings for make_shared
  msg "Fixes for boost"
  msg2 "1"
  git apply --whitespace=fix ../qt_lib.patch
  msg2 "2"
  git apply --whitespace=fix ../boost.patch

  # Gentoo fixes
  msg "Gentoo fixes"
  # http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/sci-mathematics/rstudio/
  git apply ../prefs.patch

  # make sure icons and mime stuff are with prefix
  sed -i \
    -e "s:/usr:$pkgdir/usr:g" \
    CMakeGlobals.txt src/cpp/desktop/CMakeLists.txt || die

  # get version information from git
  cd "$srcdir/$_gitname"
  _gittag=$(git describe --tags $(git rev-list --tags --max-count=1))

  gittag=${_gittag#v*}
  major=${gittag%.*.*}
  gittag=${gittag#$major.*}
  minor=${gittag%.*}
  patch=${gittag#*.}

  echo "${major}.${minor}.${patch}"

  msg "install-packages and Pandoc"
  cd "$srcdir/$_gitname/dependencies/common"
  # Create empty dictionaries folder
  mkdir -p "dictionaries"
  touch "dictionaries/.aff"
  touch "dictionaries/.dic"

  # get pandocver and nodever install-{pandoc,npm-dependencies}
  pandocver=$(grep "PANDOC_VERSION=" install-pandoc | cut -d '"' -f2)
  nodever=$(grep "NODE_VERSION=" install-npm-dependencies | cut -d '"' -f2)
  
  # Add mathjax & pandoc (in theory pandoc version differ, but - unless something 
  # new is added to a pandoc dependent package - it does not matter)
  rm -rf "pandoc/" && mkdir -p "pandoc/$pandocver"
  ln -s "/usr/share/mathjax2" "mathjax-27"
  ln -s "/usr/bin/pandoc" "pandoc/$pandocver/pandoc"
  ./install-packages

  # Nodejs: use any nodejs available (legacy or current does not seem to matter)
  mkdir -p node/${nodever}/{bin,include}
  ln -s /usr/bin/node node/${nodever}/bin
  ln -s /usr/include/node node/${nodever}/include
  cd "${srcdir}/${_gitname}/src/gwt/panmirror/src/editor"
  yarn config set ignore-engines true
  yarn install

  # version information in rstudio
  sed -i -e "s/CPACK_PACKAGE_VERSION_MAJOR \"99\"/CPACK_PACKAGE_VERSION_MAJOR \"$major\"/g; \
             s/CPACK_PACKAGE_VERSION_MINOR \"9\"/CPACK_PACKAGE_VERSION_MINOR \"$minor\"/g; \
             s/CPACK_PACKAGE_VERSION_PATCH \"9\"/CPACK_PACKAGE_VERSION_PATCH \"$patch\"/g" \
             "$srcdir/$_gitname/CMakeGlobals.txt"

}

build(){

  cmake -B build -S $srcdir/$_gitname \
        -DRSTUDIO_TARGET=Desktop \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio \
        -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake \
        -DRSTUDIO_USE_SYSTEM_BOOST=yes \
        -DRSTUDIO_USE_SYSTEM_SOCI=yes \
        -DRSTUDIO_USE_SYSTEM_YAML_CPP=yes\
        ..

  msg "Starting make"
  cmake --build build -j16
}

package() {

  msg "make install"
  DESTDIR="$pkgdir" cmake --install build

  rm -rf "$pkgdir/usr/lib/rstudio/R/packages/"*.tar.gz

  install -Dm644 "$srcdir/rstudio.desktop" "$pkgdir/usr/share/applications/rstudio.desktop"
  install -Dm644 "$srcdir/rstudio.png" "$pkgdir/usr/share/pixmaps/rstudio.png"

  msg "Cleaning Sources …"
  rm -rf "$srcdir/$_gitname/build"
  rm -r "$srcdir/$_gitname/dependencies/common/dictionaries/"
  rm -r "$srcdir/$_gitname/dependencies/common/mathjax-27"
}

# vim:set ts=2 sw=2 et:
