# Maintainer: Jan Marvin Garbuszus <jan.garbuszus@ruhr-uni-bochum.de>
# Inspired by https://aur.archlinux.org/packages.php?ID=47225
#

verdate=$(date +"%Y%m%d")

pkgname=rstudio-desktop-git
pkgver=$verdate
pkgrel=1
pkgdesc="rstudio git release pkgbuild by jmg"
arch=('x86_64')
url="http://www.rstudio.org/"
license=('AGPL3')
depends=('r' 'boost-libs' 'clang' 'postgresql-libs' 'quarto' 'soci' 'sqlite') # 'yaml-cpp'
makedepends=('git' 'cmake' 'jdk-openjdk' 'apache-ant' 'unzip' 'openssl' 'pam' 'mathjax2' 'rrdtool' 'yarn' 'nodejs' 'typescript')
provides=('rstudio-desktop')
conflicts=('rstudio-desktop')
install=rstudio.install
source=('rstudio::git+https://github.com/rstudio/rstudio.git'
        'rstudio.desktop'
        'rstudio.png'
        'yaml_cmake.diff'
        )
md5sums=('SKIP'
         '07b7333a588e5629f86b7dc3fba79ecf'
         '99ad4e9914f50ecec97e5cdb4001ad29'
         '3ea50bedd4f294c82ed78eca19ba8ada')
options=(!debug)

prepare() {
  cd "$srcdir/rstudio"
  git reset --hard

  git apply ../yaml_cmake.diff

  sed -i \
    -e "s:/usr:$pkgdir/usr:g" \
    cmake/globals.cmake || die

  sed -i '/add_definitions(-D_FORTIFY_SOURCE=2)/d' $srcdir/rstudio/src/cpp/CMakeLists.txt

  msg "install-packages and Pandoc"
  cd "$srcdir/rstudio/dependencies/"
  # Create empty dictionaries folder
  mkdir -p "dictionaries"
  touch "dictionaries/.aff"
  touch "dictionaries/.dic"

  # get nodever ... grml
  nodever=$(grep "RSTUDIO_NODE_VERSION=" tools/rstudio-tools.sh | cut -d '"' -f2)
  nodever_installed=$(grep "RSTUDIO_INSTALLED_NODE_VERSION=" tools/rstudio-tools.sh | cut -d '"' -f2)
  nodever_installed="${nodever_installed}-installed"

  # Add mathjax & quarto (this #!@*&% does not bother me at all)
  rm -rf "mathjax-27" 
  ln -s "/usr/share/mathjax2" "mathjax-27"

  rm -rf "quarto"
  if [[ $(which quarto) == "/usr/local/bin/quarto" ]] 
  then
    ln -s "/usr/local/lib/quarto" .
  else
    ln -s "/opt/quarto" .
  fi

  cd "common/"
  ./install-boost
  ./install-copilot-language-server
  ./install-packages
  ./install-panmirror
  ./install-gwt

  # Nodejs: use any nodejs available (legacy or current does not seem to matter)
  rm -rf node
  mkdir -p node/${nodever}/{bin,include}
  ln -s /usr/bin/node node/${nodever}/bin
  ln -s /usr/bin/npm node/${nodever}/bin
  ln -s /usr/bin/npx node/${nodever}/bin
  ln -s /usr/include/node node/${nodever}/include

  mkdir -p node/${nodever_installed}/bin
  ln -s /usr/bin/node node/${nodever_installed}/bin

  yarn config set ignore-engines true
  yarn install --arch=x64

  # fix packages out of sync
  cd $srcdir/rstudio/src/node/desktop
  npm install

}

build(){

  export NODE_OPTIONS="--localstorage-file=$srcdir/rstudio-build-localstorage"
  touch "$srcdir/rstudio-build-localstorage"

  # archlinux-java set java-23-openjdk
  cmake -B build -S $srcdir/rstudio \
        -DRSTUDIO_TARGET=Electron \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio \
        -DRSTUDIO_USE_SYSTEM_SOCI=yes \
        -DRSTUDIO_INSTALL_FREEDESKTOP=yes \
        -DRSTUDIO_USE_SYSTEM_YAML_CPP=yes

  msg "Starting make"
  cmake --build build -j16
  
}

package() {

  msg "make install"
  DESTDIR="$pkgdir" cmake --install build

  rm -rf "$pkgdir/usr/lib/rstudio/R/packages/"*.tar.gz

  install -Dm644 "$srcdir/rstudio.desktop" "$pkgdir/usr/share/applications/rstudio.desktop"
  install -Dm644 "$srcdir/rstudio.png" "$pkgdir/usr/share/pixmaps/rstudio.png"

  msg "Cleaning Sources â€¦"
  rm -rf "$srcdir/rstudio/build"
  rm -r "$srcdir/rstudio/dependencies/dictionaries/"
  rm -r "$srcdir/rstudio/dependencies/mathjax-27"
}

# vim:set ts=2 sw=2 et:
