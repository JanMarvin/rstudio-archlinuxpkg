# Maintainer: Jan Marvin Garbuszus <jan.garbuszus@ruhr-uni-bochum.de>
# Inspired by https://aur.archlinux.org/packages.php?ID=47225
#

verdate=$(date +"%Y%m%d")

pkgname=rstudio-desktop-git
pkgver=$verdate
pkgrel=1
pkgdesc="rstudio git release pkgbuild by jmg"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('AGPL3')
depends=('r>=3.0.1' 'boost-libs>=1.50' 'clang' 'soci' 'postgresql-libs' 'sqlite')
makedepends=('git' 'cmake>=2.6' 'boost>=1.50' 'java-environment' 'apache-ant' 'unzip' 'openssl' 'pam' 'mathjax2' 'rrdtool' 'pandoc')
provides=('rstudio-desktop')
conflicts=('rstudio-desktop')
install=rstudio.install
source=('rstudio.desktop'
        'rstudio.png'
        'cursor_fix.diff'
        'prefs.patch'
        'qt511.patch'
        'boost.patch')

md5sums=('07b7333a588e5629f86b7dc3fba79ecf'
         '99ad4e9914f50ecec97e5cdb4001ad29'
         'bb9bfbb9f24185b334a0d39e8cab2200'
         'b7ca2e015536f4df360b26322ccc2d72'
         '550b1e8a983906b0754174c03ba6d025'
         '83df084fa6423696d67aad300f6474c0')

_gitroot="git://github.com/rstudio/rstudio.git"
_gitname="rstudio"

prepare() {
  cd $srcdir/..

  # Fetch Sources
  if [ -d "$_gitname" ] ; then
    msg "Updating repository from server …"
    cd "$_gitname"
    # git reset required for the sed-gin-hack
    git reset --hard && git pull

    git checkout master
    # git checkout v1.3
  else
    msg "Cloning from GIT server …"
    git clone "$_gitroot" "$_gitname"
  fi

  cd "$srcdir"
  rm -rf rstudio
  git clone ../$_gitname
  cd $_gitname

  # boost fixes. boostsignals2 and includings for make_shared
  msg "Fixes for boost"
  msg2 "1"
  git apply --whitespace=fix ../cursor_fix.diff
  msg2 "2"
  git apply --whitespace=fix ../qt511.patch
  msg2 "3"
  git apply --whitespace=fix ../boost.patch

  # Gentoo fixes
  msg "Gentoo fixes"
  # http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/sci-mathematics/rstudio/
  git apply ../prefs.patch

  # make sure icons and mime stuff are with prefix
  sed -i \
    -e "s:/usr:$pkgdir/usr:g" \
    CMakeGlobals.txt src/cpp/desktop/CMakeLists.txt || die

  # get version information from git
  cd "$srcdir/$_gitname"
  _gittag=$(git describe --tags $(git rev-list --tags --max-count=1))

  gittag=${_gittag#v*}
  major=${gittag%.*.*}
  gittag=${gittag#$major.*}
  minor=${gittag%.*}
  patch=${gittag#*.}

  echo "${major}.${minor}.${patch}"

  msg "install-packages and Pandoc"
  cd $srcdir/$_gitname/dependencies/common/
  ./install-packages
  ./install-npm-dependencies # hardcoded nodejs version. can this be avoided?
  rm -rf "pandoc/" && mkdir pandoc

  # Create empty dictionaries folder
  mkdir -p "$srcdir/$_gitname/dependencies/common/dictionaries"
  touch "$srcdir/$_gitname/dependencies/common/dictionaries/.aff"
  touch "$srcdir/$_gitname/dependencies/common/dictionaries/.dic"

  # Add mathjax
  ln -s "/usr/share/mathjax2" "mathjax-27"
  ln -s "/usr/bin/pandoc" "pandoc/pandoc"
  ln -s "/usr/bin/pandoc-citeproc" "pandoc/pandoc-citeproc"

  # Desktop, Release
  cd "$srcdir/$_gitname"
  mkdir build && cd build

  # version information in rstudio
  sed -i -e "s/CPACK_PACKAGE_VERSION_MAJOR \"99\"/CPACK_PACKAGE_VERSION_MAJOR \"$major\"/g; \
             s/CPACK_PACKAGE_VERSION_MINOR \"9\"/CPACK_PACKAGE_VERSION_MINOR \"$minor\"/g; \
             s/CPACK_PACKAGE_VERSION_PATCH \"9\"/CPACK_PACKAGE_VERSION_PATCH \"$patch\"/g" \
             "$srcdir/$_gitname/CMakeGlobals.txt"

}

build(){
  LANG=POSIX

  cd "$srcdir/$_gitname/build/"

  msg "Starting cmake …"
  cmake -DRSTUDIO_TARGET=Desktop \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio \
        -DQT_QMAKE_EXECUTABLE=/usr/lib/qmake \
        -DRSTUDIO_USE_SYSTEM_BOOST=yes \
        -DRSTUDIO_USE_SYSTEM_SOCI=yes \
        ..

  msg "Starting make"
  make -j16
}

package() {
  LANG=POSIX
  cd "$srcdir/$_gitname/build/"

  msg "make install"
  make DESTDIR="$pkgdir/" install

  rm -rf "$pkgdir/usr/lib/rstudio/R/packages/"*.tar.gz

  install -Dm644 "$srcdir/rstudio.desktop" "$pkgdir/usr/share/applications/rstudio.desktop"
  install -Dm644 "$srcdir/rstudio.png" "$pkgdir/usr/share/pixmaps/rstudio.png"

  # link pandoc (again)
  mkdir "$pkgdir/usr/lib/rstudio/bin/pandoc"
  ln -s "/usr/bin/pandoc" "$pkgdir/usr/lib/rstudio/bin/pandoc/pandoc"
  ln -s "/usr/bin/pandoc-citeproc" "$pkgdir/usr/lib/rstudio/bin/pandoc/pandoc-citeproc"

  msg "Cleaning Sources …"
  rm -rf "$srcdir/$_gitname/build"
  rm -r "$srcdir/$_gitname/dependencies/common/dictionaries/"
  rm -r "$srcdir/$_gitname/dependencies/common/mathjax-27"
}

# vim:set ts=2 sw=2 et:
