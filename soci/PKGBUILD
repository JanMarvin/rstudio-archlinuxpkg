# Maintainer: Jan Marvin Garbuszus <jan.garbuszus@ruhr-uni-bochum.de>
# Contributor:  Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Daniel Nagy <danielnagy at gmx de>
# Contributor: Mika Fischer <mika.fischer@zoopnet.de>

pkgname=soci
pkgver=4.0.3
pkgrel=5
pkgdesc="The C++ Database Access Library"
arch=('x86_64')
url="http://soci.sf.net"
license=('custom:boost')
depends=('gcc-libs')
makedepends=('cmake' 'postgresql-libs' 'sqlite3' 'unixodbc' 'boost')
optdepends=('oracle-instantclient-basic: support for oracle databases'
            'libmysqlclient: support for mysql databases'
            'postgresql-libs: support for postgresql databases'
            'sqlite3: support for sqlite databases'
            'unixodbc: support for ODBC databases'
            'libmysqlclient'
            'boost-libs')
source=("http://downloads.sourceforge.net/project/soci/soci/$pkgname-$pkgver/$pkgname-$pkgver.zip")
sha1sums=('e76d5862f90cfa8a0f9fe6ce477c3c7fa669af62')

prepare() {
    cmake -B build -S $srcdir/$pkgname-$pkgver \
        -DSOCI_TESTS=OFF \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DSOCI_SHARED=ON \
        -DSOCI_STATIC=ON \
        -DCMAKE_CXX_STANDARD=11
}

build() {
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build

    install -Dm0644 "$srcdir/$pkgname-$pkgver/LICENSE_1_0.txt" \
        "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
