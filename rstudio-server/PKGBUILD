# Maintainer: Jan Marvin Garbuszus <jan.garbuszus@ruhr-uni-bochum.de>
# Inspired by https://aur.archlinux.org/packages.php?ID=47225
#

verdate=$(date +"%Y%m%d")

pkgname=rstudio-server-git
_pkgnames=rstudio-server
pkgver=$verdate
pkgrel=1
pkgdesc="rstudio git release pkgbuild by jmg"
arch=('x86_64')
url="http://www.rstudio.org/"
license=('AGPL3')
depends=('r' 'clang' 'soci' 'postgresql-libs' 'quarto')
makedepends=('git' 'cmake' 'boost' 'jdk-openjdk' 'apache-ant' 'unzip' 'openssl' 'pam' 'mathjax2' 'rrdtool' 'nodejs' 'yarn')
provides=('rstudio-server')
conflicts=('rstudio-server')
install=rstudio-server.install
source=('rstudio::git+https://github.com/rstudio/rstudio.git'
        'rstudio-server.service'
        'rstudio-server.sysusers'
        'rstudio-server.tmpfiles'
        'cmake.diff')
md5sums=('SKIP'
         '6c48d54eae2436b4021106d976ee6b2c'
         '0140e5b2fbc96a62ace589f121cab45f'
         '63b8c7f91d3a019260191e545bde328f'
         'abb09217dd627d0b68f5838f0a2cb427')
options=(!debug)

prepare() {
  cd "$srcdir/rstudio"
  git reset --hard

  git apply ../cmake.diff

  # make sure icons and mime stuff are with prefix
  sed -i \
    -e "s:/usr:$pkgdir/usr:g" \
    CMakeGlobals.txt src/cpp/desktop/CMakeLists.txt || die

  msg "install-packages and Pandoc"
  cd "$srcdir/rstudio/dependencies/"
  # Create empty dictionaries folder
  mkdir -p "dictionaries"
  touch "dictionaries/.aff"
  touch "dictionaries/.dic"

  # get nodever 
  nodever=$(grep "RSTUDIO_NODE_VERSION=" tools/rstudio-tools.sh | cut -d '"' -f2)

  # Add mathjax & quarto (this #!@*&% does not bother me at all)
  rm -rf "mathjax-27"
  ln -s "/usr/share/mathjax2" "mathjax-27"

  rm -rf "quarto"
  if [[ $(which quarto) == "/usr/local/bin/quarto" ]] 
  then
    ln -s "/usr/local/lib/quarto" .
  else
    ln -s "/opt/quarto" .
  fi

  cd "common/"
  ./install-boost
  ./install-packages
  ./install-panmirror

  # Nodejs
  rm -rf node
  mkdir -p node/${nodever}/{bin,include}
  ln -s /usr/bin/node node/${nodever}/bin
  ln -s /usr/bin/npm node/${nodever}/bin
  ln -s /usr/bin/npx node/${nodever}/bin
  ln -s /usr/include/node node/${nodever}/include
  yarn config set ignore-engines true
  yarn install

  # version information in rstudio
  sed -i -e "s/CPACK_PACKAGE_VERSION_MAJOR \"99\"/CPACK_PACKAGE_VERSION_MAJOR \"$major\"/g; \
             s/CPACK_PACKAGE_VERSION_MINOR \"9\"/CPACK_PACKAGE_VERSION_MINOR \"$minor\"/g; \
             s/CPACK_PACKAGE_VERSION_PATCH \"9\"/CPACK_PACKAGE_VERSION_PATCH \"$patch\"/g" \
             "$srcdir/rstudio/CMakeGlobals.txt"

}

build(){

  # https://github.com/rstudio/rstudio/issues/9463
  export PATH=/usr/lib/jvm/java-11-openjdk/bin/:${PATH}

  msg "Starting cmake â€¦"
  cmake -B build -S $srcdir/rstudio \
        -DRSTUDIO_TARGET=Server \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio-server \
        -DRSTUDIO_USE_SYSTEM_SOCI=yes \
        -DRSTUDIO_USE_SYSTEM_YAML_CPP=yes

  msg "Starting make"
  cmake --build build -j16
}

package() {

  msg "make install"
  DESTDIR="$pkgdir/" cmake --install build

  rm -rf "$pkgdir/usr/lib/$_pkgnames/R/packages/"*.tar.gz

  # 02210 is boston
  _gid_log=19
  _gid_rstudio_server=2210
  _uid_rstudio_server=2210

  # Create config directory
  install -dm  775 "$pkgdir/etc/rstudio/"

  # Create var directories
  install -dm 1777 "$pkgdir/run/$_pkgnames"
  install -dm  775 "$pkgdir/run/lock/$_pkgnames"
  install -dm  775 "$pkgdir/var/lib/$_pkgnames"
  install -dm  775 "$pkgdir/var/lib/$_pkgnames/"{conf,body,proxy}
  install -dm  775 -o ${_uid_rstudio_server} -g ${_gid_log} "$pkgdir/var/log/$_pkgnames"

  chown -R ${_uid_rstudio_server}:${_gid_rstudio_server} "$pkgdir/run/$_pkgnames" 
  chown -R ${_uid_rstudio_server}:${_gid_rstudio_server} "$pkgdir/var/lib/$_pkgnames" 

  # PAM config
  install -dm  755 "$pkgdir/etc/pam.d"
  install -Dm  644 "$pkgdir/usr/lib/$_pkgnames/extras/pam/rstudio" "$pkgdir/etc/pam.d/rstudio"

  # Systemd script
  install -Dm  644 "$srcdir/$_pkgnames.service"  "$pkgdir/usr/lib/systemd/system/$_pkgnames.service"
  install -Dm  644 "$srcdir/$_pkgnames.sysusers" "$pkgdir/usr/lib/sysusers.d/$_pkgnames.conf"
  install -Dm  644 "$srcdir/$_pkgnames.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$_pkgnames.conf"

  # Create symlink for admin script
  install -dm  755 "$pkgdir/usr/bin"
  ln -sv "/usr/lib/$_pkgnames/bin/$_pkgnames" "$pkgdir/usr/bin/$_pkgnames"

}

# vim:set ts=2 sw=2 et:
