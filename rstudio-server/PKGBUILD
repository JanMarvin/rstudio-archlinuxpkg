# Maintainer: Jan Marvin Garbuszus <jan.garbuszus@ruhr-uni-bochum.de>
# Inspired by https://aur.archlinux.org/packages.php?ID=47225
#

verdate=$(date +"%Y%m%d")

pkgname=rstudio-server-git
_pkgnames=rstudio-server
pkgver=$verdate
pkgrel=1
pkgdesc="rstudio git release pkgbuild by jmg"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('AGPL3')
depends=('r>=3.0.1' 'boost-libs>=1.50' 'clang' 'soci' 'postgresql-libs' 'pandoc')
makedepends=('git' 'cmake>=2.6' 'boost>=1.50' 'java-environment' 'apache-ant' 'unzip' 'openssl' 'pam' 'mathjax2' 'rrdtool')
provides=('rstudio-server')
conflicts=('rstudio-server')
install=rstudio-server.install
source=('rstudio-server.service'
        'prefs.patch'
        'boost.patch')

md5sums=('b40ceae6b1f5a6b881a06b6d2ca4748d'
         'b7ca2e015536f4df360b26322ccc2d72'
         '83df084fa6423696d67aad300f6474c0')

_gitroot="git://github.com/rstudio/rstudio.git"
_gitname="rstudio"

prepare() {
  cd $srcdir/..

  # Fetch Sources
  if [ -d "$_gitname" ] ; then
    msg "Updating repository from server …"
    cd "$_gitname"
    # git reset required for the sed-gin-hack
    git reset --hard && git pull

    git checkout master
  else
    msg "Cloning from GIT server …"
    git clone "$_gitroot" "$_gitname"
  fi

  cd "$srcdir"
  rm -rf rstudio
  git clone ../$_gitname
  cd $_gitname

  msg2 "1"
  git apply --whitespace=fix ../boost.patch

  # Gentoo fixes
  msg "Gentoo fixes"
  # http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/sci-mathematics/rstudio/
  git apply ../prefs.patch

  # make sure icons and mime stuff are with prefix
  sed -i \
    -e "s:/usr:$pkgdir/usr:g" \
    CMakeGlobals.txt src/cpp/desktop/CMakeLists.txt || die

  # get version information from git
  cd "$srcdir/$_gitname"
  _gittag=$(git describe --abbrev=0 --tags)

  gittag=${_gittag#v*}
  major=${gittag%.*.*}
  gittag=${gittag#$major.*}
  minor=${gittag%.*}
  patch=${gittag#*.}

  echo "${major}.${minor}.${patch}"

  msg "install-packages and Pandoc"
  cd "$srcdir/$_gitname/dependencies/common/"
  ./install-packages
  ./install-npm-dependencies
  rm -rf "pandoc/" && mkdir -p "pandoc/2.11.0.2"

  # Create empty dictionaries folder
  mkdir -p "dictionaries"
  touch "dictionaries/.aff"
  touch "dictionaries/.dic"

  # Add mathjax & pandoc
  ln -s "/usr/share/mathjax2" "mathjax-27"
  ln -s "/usr/bin/pandoc" "pandoc/2.11.0.2"

  # Server, Release
  cd "$srcdir/$_gitname"
  mkdir build && cd build

  # version information in rstudio
  sed -i -e "s/CPACK_PACKAGE_VERSION_MAJOR \"99\"/CPACK_PACKAGE_VERSION_MAJOR \"$major\"/g; \
             s/CPACK_PACKAGE_VERSION_MINOR \"9\"/CPACK_PACKAGE_VERSION_MINOR \"$minor\"/g; \
             s/CPACK_PACKAGE_VERSION_PATCH \"9\"/CPACK_PACKAGE_VERSION_PATCH \"$patch\"/g" \
             "$srcdir/$_gitname/CMakeGlobals.txt"

}

build(){
  LANG=POSIX

  cd "$srcdir/$_gitname/build/"

  msg "Starting cmake …"
  cmake -DRSTUDIO_TARGET=Server \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio-server \
        -DRSTUDIO_USE_SYSTEM_BOOST=yes \
        -DRSTUDIO_USE_SYSTEM_SOCI=yes \
        ..

  msg "Starting make"
  make -j16
}

package() {
  LANG=POSIX
  cd "$srcdir/$_gitname/build/"

  msg "make install"
  make DESTDIR="$pkgdir/" install

  rm -rf "$pkgdir/usr/lib/rstudio/R/packages/"*.tar.gz

  # Create config directory
  install -dm755 "$pkgdir/etc/rstudio/"

  # Create var directories
  install -dm755 "$pkgdir/run/$_pkgnames"
  install -dm755 "$pkgdir/run/lock/$_pkgnames"
  install -dm755 "$pkgdir/var/log/$_pkgnames"
  install -dm755 "$pkgdir/var/lib/$_pkgnames"
  install -dm755 "$pkgdir/var/lib/$_pkgnames/conf"
  install -dm755 "$pkgdir/var/lib/$_pkgnames/body"
  install -dm755 "$pkgdir/var/lib/$_pkgnames/proxy"

  # PAM config
  install -dm755 "$pkgdir/etc/pam.d"
  install -Dm644 "$pkgdir/usr/lib/$_pkgnames/extras/pam/rstudio" "$pkgdir/etc/pam.d/rstudio"

  # Systemd script
  install -Dm644 "$srcdir/$_pkgnames.service" "$pkgdir/usr/lib/systemd/system/$_pkgnames.service"

  # Create symlink for admin script
  install -dm755 "$pkgdir/usr/bin"
  ln -sv "/usr/lib/$_pkgnames/bin/$_pkgnames" "$pkgdir/usr/bin/$_pkgnames"

  # link pandoc (again)
  mkdir -p "$pkgdir/usr/lib/$_pkgnames/bin/pandoc/2.11.0.2"
  ln -s "/usr/bin/pandoc" "$pkgdir/usr/lib/$_pkgnames/bin/pandoc/2.11.0.2"

  msg "Cleaning Sources …"
  rm -rf "$srcdir/$_gitname/build"
  rm -r "$srcdir/$_gitname/dependencies/common/dictionaries/"
  rm -r "$srcdir/$_gitname/dependencies/common/mathjax-27"
}

# vim:set ts=2 sw=2 et:
