# Inspired by https://aur.archlinux.org/packages.php?ID=47225
#

verdate=$(date +"%Y%m%d")

pkgname=rstudio-server-git
_pkgnames=rstudio-server
pkgver=$verdate
pkgrel=1
pkgdesc="rstudio git release pkgbuild by jmg"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('AGPL3')
depends=('r>=3.0.1' 'qt4>=4.7' 'boost-libs>=1.50' 'clang')
makedepends=('git' 'cmake>=2.6' 'boost>=1.50' 'java-environment' 'apache-ant' 'unzip' 'openssl' 'pam' 'gin' 'gwt' 'mathjax' 'rrdtool' 'pandoc' 'libc++')
provides=('rstudio-server')
conflicts=('rstudio-server')
install=rstudio-server.install
source=('rstudio-server.service'
        'make_shared.patch'
        'boostsignals.patch'
        'prefs.patch')

md5sums=('6622f79d2a871c74e879043202696412'
         '9c5504ec85d0897845b8dfc105788b3f'
         'e24f437e8269b674f6abddf7c9f15669'
         '43f0ff6044f0d35123abdf2fe2283861')

_gitroot="git://github.com/rstudio/rstudio.git"
_gitname="rstudio"

prepare() {
  cd $srcdir/..

  # Fetch Sources
  if [ -d "$_gitname" ] ; then
    msg "Updating repository from server …" 
    cd "$_gitname"
    # git reset required for the sed-gin-hack
    git reset --hard && git pull
  else
    msg "Cloning from GIT server …"
    git clone "$_gitroot" "$_gitname"
  fi
  
  # build rmarkdown outside of the build tree
  cd $srcdir/../rstudio/dependencies/common
  msg "Install additional packages (install-packages)"
  ./install-packages

  # srcdir
  cd "$srcdir"
  rm -rf rstudio
  git clone ../$_gitname
  cd $_gitname
  git apply --whitespace=fix ../boostsignals.patch

  # Gentoo fixes
  msg "Gentoo fixes"
  # http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/sci-mathematics/rstudio/
  git apply ../prefs.patch

  # make sure icons and mime stuff are with prefix
  sed -i \
    -e "s:/usr:$pkgdir/usr:g" \
    CMakeGlobals.txt src/cpp/desktop/CMakeLists.txt || die
	
  cd ..


  # get version information from git
    cd "$srcdir/$_gitname"
    _gittag=$(git describe --abbrev=0 --tags)

    gittag=${_gittag#v*}
    major=${gittag%.*.*}
    gittag=${gittag#$major.*}
    minor=${gittag%.*}
    patch=${gittag#*.}

    echo "${major}.${minor}.${patch}"

  # Additional Requirements
  
  msg "GWT"
  mkdir "$srcdir/$_gitname/src/gwt/lib"
  ln -s /opt/gwt $srcdir/$_gitname/src/gwt/lib 
  
  msg "GIN"
  # build: requires gin 1.5
  ln -s /opt/gin $srcdir/$_gitname/src/gwt/lib
  
  msg "JUNIT"
  # ToDo: hardcoded junit number
  ln -s /opt/junit/junit-4.93b.jar $srcdir/$_gitname/src/gwt/lib
  
  msg "Pandoc"
  cd $srcdir/$_gitname/dependencies/common/
  rm -rf "pandoc/" && mkdir pandoc && cd pandoc
  ln -s "/usr/bin/pandoc" .
  
  msg "rmarkdown"
  # Copy rmarkdown.tar.gz (folder is empty and not needed, cmake checks for it. These packages are not included.)
  mkdir -p "$srcdir/$_gitname/dependencies/common/rmarkdown"
  cp "$srcdir/../$_gitname/dependencies/common/"rmarkdown_*.tar.gz "$srcdir/$_gitname/dependencies/common"
  # Copy packrat
  mkdir -p "$srcdir/$_gitname/dependencies/common/packrat"
  cp "$srcdir/../$_gitname/dependencies/common/"packrat_*.tar.gz "$srcdir/$_gitname/dependencies/common"
  # Copy rsconnect
  mkdir -p "$srcdir/$_gitname/dependencies/common/rsconnect"
  cp "$srcdir/../$_gitname/dependencies/common/"rsconnect_*.tar.gz "$srcdir/$_gitname/dependencies/common"
  ## Copy shiny
  mkdir -p "$srcdir/$_gitname/dependencies/common/shinyapps"
  cp "$srcdir/../$_gitname/dependencies/common/"shinyapps_*.tar.gz "$srcdir/$_gitname/dependencies/common"

  mkdir -p "$srcdir/$_gitname/dependencies/common/dictionaries"
  mkdir -p "$srcdir/$_gitname/dependencies/common/mathjax-23"
  ln -s "/opt/mathjax/"* "$srcdir/$_gitname/dependencies/common/mathjax-23"

  # Clang
  mkdir -p "$srcdir/$_gitname/dependencies/common/libclang/3.5"
  ln -s "/usr/lib/libclang.so" "$srcdir/$_gitname/dependencies/common/libclang/3.5"

  mkdir -p "$srcdir/$_gitname/dependencies/common/libclang/builtin-headers/3.5"
  ln -s "/usr/lib/clang/3.5.0/include/"* "$srcdir/$_gitname/dependencies/common/libclang/builtin-headers/3.5"
  mkdir -p "$srcdir/$_gitname/dependencies/common/libclang/builtin-headers/libc++/3.5"
  ln -s "/usr/include/c++/v1/"* "$srcdir/$_gitname/dependencies/common/libclang/builtin-headers/libc++/3.5"

  # Server, Release 
  cd "$srcdir/$_gitname"
  mkdir build && cd build

  # another sed hack for correct version information in rstudio
  sed -i -e "s/CPACK_PACKAGE_VERSION_MAJOR \"99\"/CPACK_PACKAGE_VERSION_MAJOR \"$major\"/g; \
             s/CPACK_PACKAGE_VERSION_MINOR \"9\"/CPACK_PACKAGE_VERSION_MINOR \"$minor\"/g; \
             s/CPACK_PACKAGE_VERSION_PATCH \"9\"/CPACK_PACKAGE_VERSION_PATCH \"$patch\"/g" \
             "$srcdir/$_gitname/CMakeGlobals.txt"

}

build(){
  LANG=POSIX

  cd "$srcdir/$_gitname/build/"
  
  msg "Starting cmake …"
  #-DQT_QMAKE_EXECUTABLE=/usr/lib/qt/qmake \
  cmake -DRSTUDIO_TARGET=Server \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio-server \
        ..

  msg "Starting make"

  make -j4
}

package() {
    cd "$srcdir/$_gitname/build/"

    msg "make install"
    make DESTDIR="$pkgdir/" install

    # Create config directory
    install -dm755 "$pkgdir/etc/rstudio/"
    
    # Create var directories
    install -dm755 "$pkgdir/run/$_pkgnames"
    install -dm755 "$pkgdir/run/lock/$_pkgnames"
    install -dm755 "$pkgdir/var/log/$_pkgnames"
    install -dm755 "$pkgdir/var/lib/$_pkgnames"
    install -dm755 "$pkgdir/var/lib/$_pkgnames/conf"
    install -dm755 "$pkgdir/var/lib/$_pkgnames/body"
    install -dm755 "$pkgdir/var/lib/$_pkgnames/proxy"
    
    # PAM config
    install -dm755 "$pkgdir/etc/pam.d"
    install -Dm644 "$pkgdir/usr/lib/$_pkgnames/extras/pam/rstudio" "$pkgdir/etc/pam.d/rstudio"
    
    # Systemd script
    install -Dm644 "$srcdir/$_pkgnames.service" "$pkgdir/usr/lib/systemd/system/$_pkgnames.service"
    
    # Create symlink for admin script
    install -dm755 "$pkgdir/usr/bin"
    ln -sv "/usr/lib/$_pkgnames/bin/$_pkgnames" "$pkgdir/usr/bin/$_pkgnames"
    
    # Cleaning sources
    msg "Cleaning Sources …"
    rm -rf "$srcdir/$_gitname/build"
    rm "$srcdir/$_gitname/src/gwt/lib/gwt"
    rm "$srcdir/$_gitname/src/gwt/lib/gin"
    rm $srcdir/$_gitname/src/gwt/lib/junit-4.93b.jar
    rm -r "$srcdir/$_gitname/src/gwt/lib"
    rm -r "$srcdir/$_gitname/dependencies/common/dictionaries/"
    rm -r "$srcdir/$_gitname/dependencies/common/mathjax-23"
}
